plugins {
    id "java"
    id "jacoco"
}

group "ru.smax.trial"
version "1.0-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    def version = [
            jackson: '2.9.7',
            lombok : '1.16.10',

            slf4j  : '1.7.25',
            log4j  : '1.2.17',

            hsqldb : '2.4.1',
            sql2o  : '1.6.0',

            guice  : '4.2.2',
            spark  : '2.8.0',

            junit  : '4.12',
            mockito: '2.23.4',
            sparkTest: '1.1.8',
            testCommons: 'v1.1.0'
    ]

    compile "org.projectlombok:lombok:${version.lombok}"
    compile "com.fasterxml.jackson.core:jackson-core:${version.jackson}"
    compile "com.fasterxml.jackson.core:jackson-databind:${version.jackson}"

    // Logging
    compile "org.slf4j:slf4j-api:${version.slf4j}"
    runtime "org.slf4j:slf4j-log4j12:${version.slf4j}"
    runtime "log4j:log4j:${version.log4j}"

    // Persistence
    compile "org.hsqldb:hsqldb:${version.hsqldb}"
    compile "org.sql2o:sql2o:${version.sql2o}"

    // Frameworks
    compile "com.google.inject:guice:${version.guice}"
    compile "com.sparkjava:spark-core:${version.spark}"

    testCompile "junit:junit:${version.junit}"
    testCompile "org.mockito:mockito-core:${version.mockito}"
    testCompile "com.despegar:spark-test:${version.sparkTest}"
    testCompile "com.github.feardude:commons:${version.testCommons}"
}

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": "ru.smax.trial.revolut.MoneyTransferApplication"
    }

    baseName = project.name + '-all'

    from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task stage(dependsOn: ['fatJar'])
